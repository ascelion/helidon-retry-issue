
plugins {
	id 'java'
	id 'application'
	id 'org.galaxx.gradle.jandex' version '1.0.2'
}

repositories {
	jcenter()
}

application {
	mainClassName = 'io.helidon.microprofile.server.Main'
}

sourceSets {
	all {
		output.resourcesDir = output.classesDirs[0]
	}
}

dependencies {
	implementation platform( 'io.helidon:helidon-bom:1.3.1' )
	implementation platform( 'io.helidon:helidon-dependencies:1.3.1' )

	implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:2.0.2'
	implementation 'jakarta.ws.rs:jakarta.ws.rs-api:2.1.6'
	implementation 'org.eclipse.microprofile.rest.client:microprofile-rest-client-api'
	implementation 'org.eclipse.microprofile.fault-tolerance:microprofile-fault-tolerance-api'

	implementation 'org.glassfish.jersey.core:jersey-server'

	runtimeOnly 'io.helidon.microprofile.bundles:helidon-microprofile-3.0'
}

tasks.withType( AbstractArchiveTask ) {
	// strip version from archive name
	archiveVersion = ''
	duplicatesStrategy = 'exclude'
}
plugins.withType( DistributionPlugin ) {
	distributions.all {
		contents.into '/'
	}
}
